file = { SOI ~ mono_rule* ~ (rule | media)* ~ EOI }

mono_rule = { mono_start ~ mono_value ~ end }
variable  = { var_name ~ var_val ~ end }
media     = { media_start ~ block }
rule      = { selecteur ~ block }

mono_start = ${ arobase ~ word }
mono_value = ${ PUSH("\"") ~ any_quote_value ~ POP }

var_val   =  { (!end ~ ANY)* }
var_name  = ${ var_start ~ prop_name ~ separation }
var_start = _{ "--" }

block = { bracket_op ~ (propertie | variable)* ~ bracket_cl }

media_start     =  { media_op ~ media_type_cond ~ (media_logic_op ~ media_type_cond)* }
media_type_cond =  { ("all" | "print" | "screen" | media_cond) }
media_cond      =  { parenthe_op ~ (propertie | prop_name | media_range | media_if) ~ parenthe_cl }
media_range     =  { value ~ operateur ~ prop_name ~ operateur ~ value }
media_if        =  { prop_name ~ operateur ~ value }
media_logic_op  =  { ("and" | "not" | "only" | "," | "or") }
media_op        = _{ "@media" }

// problème connue pour le sélecteur enfant il faut 2 espaces min pas 1
selecteur            = ${ selecteur_atomic* ~ WHITESPACE? ~ selecteur_combinator* }
selecteur_combinator = ${ selecteur_op ~ (selecteur_atomic | selecteur_combinator)+ }
selecteur_op         =  { (list | sup | neightbour | direct_neightbour | wp) ~ WHITESPACE* }
selecteur_atomic     =  { (ps_class | class | id | balise | ps_elmnt | ps_class) }
propertie            =  { prop_name ~ separation ~ value }
wp                   =  { (" " | "\t" | "\n" | "\r")+ }

list              = { "," }
direct_neightbour = { "+" }
neightbour        = { "~" }

balise   = { num_word }
class    = { dot ~ prop_name }
id       = { hastag ~ prop_name }
ps_elmnt = { pair_colon ~ prop_name }
ps_class = { colon ~ prop_name }

any_quote_value =  { (!"\"" ~ ANY)* }
value           =  { num_word ~ num_word* ~ end }
prop_name       = ${ word ~ ("-" ~ word)* }
css_name        = @{ num_word ~ ("-" ~ num_word)* }
word            =  { ASCII_ALPHA+ }
num_word        =  { ASCII_ALPHANUMERIC+ }

operateur = @{ (sup | sup_eq | inf | inf_eq) }
sup       =  { ">" }
sup_eq    =  { ">=" }
inf       =  { "<" }
inf_eq    =  { "<=" }

dot         = _{ "." }
hastag      = _{ "#" }
colon       = _{ ":" }
pair_colon  = _{ "::" }
arobase     = _{ "@" }
end         = _{ ";" }
separation  = _{ ":" }
parenthe_op = _{ "(" }
parenthe_cl = _{ ")" }
bracket_cl  = _{ "}" }
bracket_op  = _{ "{" }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT    = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
